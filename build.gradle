plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.1'
}

group 'me.omegaweapondev'
version '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

repositories {
    mavenLocal()

    maven {
        url = uri('https://hub.spigotmc.org/nexus/content/groups/public/')
    }

    maven {
        url = uri('https://jitpack.io')
    }

    maven {
        url = uri('https://repo.codemc.org/repository/maven-public')
    }

    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
}

shadowJar {
    archiveClassifier.set('')
    relocate 'me.ou', 'me.omegaweapondev.libs.ou'
    relocate 'org.bstats', 'me.omegaweapondev.libs.bstats'
    relocate 'net.kyori', 'me.omegaweapondev.libs.adventure'
    relocate 'dev.dbassett', 'me.omegaweapondev.libs.skullcreator'
    relocate 'com.tchristofferson', 'me.omegaweapondev.libs.configupdater'
}

dependencies {
    implementation 'me.ou:library:1.6.3'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
    compileOnly 'org.spigotmc:spigot-api:1.18-R0.1-SNAPSHOT'
    compileOnly 'org.jetbrains:annotations:22.0.0'
}

tasks.processResources {
    filesMatching("plugin.yml") {
        expand(
                "name": project.name,
                "version": project.version,
                "mainClass": project.group + ".updatenotifier.UpdateNotifier",
                "apiVersion": 1.13,
                "author": 'OmegaWeaponDev'
        )
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
